// Гарант+ Килипенко 27.01.2025 [F00233937] Отчет Расчет сумм задолженности для передачи в суд ++
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
    СтандартнаяОбработка = Ложь;

    ИнициализироватьПараметры();

    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных,
            ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки, ,
            Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    МВТ = Новый МенеджерВременныхТаблиц;

    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина, Истина, МВТ);

    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    ТаблицаИсходныхДанных = ПроцессорВывода.Вывести(ПроцессорКомпоновки);

    ДанныеЗаполнения = Новый Структура;
    ДанныеЗаполнения.Вставить("ТаблицаИсходныхДанных", ТаблицаИсходныхДанных);
    ДанныеЗаполнения.Вставить("ОбщиеДанные", Новый Структура);
    ПараметрПериодОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек, "ПериодОтчета");
    ДанныеЗаполнения.ОбщиеДанные.Вставить("ПериодОтчета", ПараметрПериодОтчета.Значение);

    ПараметрВидОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек, "ВидОтчета");
    Если ПараметрВидОтчета.Использование И ПараметрВидОтчета.Значение = "Упрощенный" Тогда
        ДокументРезультат = Отчеты.ГП_РасчетЗадолженностиДляПередачиСуд.СформироватьДокументОтчетаУпрощенный(
                ДанныеЗаполнения, ДокументРезультат);
    Иначе
        ДокументРезультат = Отчеты.ГП_РасчетЗадолженностиДляПередачиСуд.СформироватьДокументОтчета(
                ДанныеЗаполнения, ДокументРезультат);
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий
// Гарант+ Килипенко 27.01.2025 [F00233937] Отчет Расчет сумм задолженности для передачи в суд --

// Гарант+ Килипенко 27.01.2025 [F00233937] Отчет Расчет сумм задолженности для передачи в суд ++
#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьПараметры()
    // Установка границ периода отчета
    УточнитьПараметрыПериодаОтчета();

    // Заполняем параметр АналитикаРасчетов
    АналитикаРасчетов = Новый СписокЗначений;
    АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
    АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
    АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "АналитикаРасчетов", АналитикаРасчетов);

    // Заполняем параметр АналитикаРасчетовВыручки
    АналитикаРасчетовВыручки = Новый СписокЗначений;
    АналитикаРасчетовВыручки.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
    АналитикаРасчетовВыручки.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС);
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "АналитикаРасчетовВыручки", АналитикаРасчетовВыручки);

    // Заполняем параметр ФильтрСписокСчетов
    ФильтрСписокСчетов = ПолучитьОсновныеСчетаУчета();
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "ФильтрСписокСчетов", ФильтрСписокСчетов);

    // Заполняем параметр СписокСчетовВыручки
    ФильтрСписокСчетов = ПолучитьСчетаУчетаВыручки();
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "СписокСчетовВыручки", ФильтрСписокСчетов);
КонецПроцедуры

Функция УточнитьПараметрыПериодаОтчета()
    РезультатФункции = Новый Структура("НачалоПериода, КонецПериода", Дата(1, 1, 1), Дата(1, 1, 1));

    ПараметрПериодОтчета =
        БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек, "ПериодОтчета");

    РезультатФункции.НачалоПериода = НачалоДня(ПараметрПериодОтчета.Значение.ДатаНачала);
    РезультатФункции.КонецПериода = КонецДня(ПараметрПериодОтчета.Значение.ДатаОкончания);

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "НачалоПериода", РезультатФункции.НачалоПериода);

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "КонецПериода", РезультатФункции.КонецПериода);

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "НачалоПериодаВключая",
        Новый Граница(РезультатФункции.НачалоПериода, ВидГраницы.Включая));

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "КонецПериодаВключая",
        Новый Граница(РезультатФункции.КонецПериода, ВидГраницы.Включая));

    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьОсновныеСчетаУчета()
    РезультатФункции = Новый СписокЗначений;

    РезультатФункции.Добавить(ПланыСчетов.Хозрасчетный.НайтиПоКоду("62"));

    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьСчетаУчетаВыручки()
    РезультатФункции = Новый СписокЗначений;

    РезультатФункции.Добавить(ПланыСчетов.Хозрасчетный.НайтиПоКоду("90.01"));

    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьОсновнойНаборДанных()
    Возврат ЭтотОбъект.СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 27.01.2025 [F00233937] Отчет Расчет сумм задолженности для передачи в суд --
