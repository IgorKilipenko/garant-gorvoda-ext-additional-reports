#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
    СтандартнаяОбработка = Ложь;

    // Инициализация параметров
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(ЭтотОбъект.КомпоновщикНастроек, "ВидДоговораАбонентскийОтдел",
        ГП_РаботаСДоговорами.ПолучитьДополнительныйВидДоговораАбонентскийОтдел().Ссылка);
    ОрганизацияПоУмолчанию = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию();
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек, "Организация", ОрганизацияПоУмолчанию);

    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных,
            ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);

    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    МВТ = СформироватьВременныеТаблицыДляОтчета(МВТ);
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина, Истина, МВТ);

    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

Функция СформироватьВременныеТаблицыДляОтчета(Знач МВТ = Неопределено)
    МВТ = ?(МВТ = Неопределено, Новый МенеджерВременныхТаблиц, МВТ);

    ПараметрПериод =
        БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек, "Период");
    ДатаАктуальности = ?(ТипЗнч(ПараметрПериод.Значение) = Тип("Дата"), ПараметрПериод.Значение, ПараметрПериод.Значение.Дата);

    ПараметрТолькоДействующиеЛС =
        БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек, "ТолькоДействующиеЛС");

    Запрос = Новый Запрос;
    Запрос.МенеджерВременныхТаблиц = МВТ;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
        |	ЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет
        |ПОМЕСТИТЬ ВТ_ДействующиеЛицевыеСчета
        |ИЗ
        |	РегистрСведений.КВП_ЛицевыеСчета.СрезПоследних(&Период, ) КАК ЛицевыеСчета
        |ГДЕ
        |	ИСТИНА
        |   И ВЫБОР
        |       КОГДА &ТолькоДействующиеЛС = ЛОЖЬ ТОГДА ИСТИНА
        |       ИНАЧЕ ЛицевыеСчета.Действует = ИСТИНА
        |   КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |    ДинамическиеПоказателиЛицевыхСчетов.ЛицевойСчет КАК ЛицевойСчет,
        |    ДинамическиеПоказателиЛицевыхСчетов.ВидПотребления КАК ВидПотребления,
        |    ДинамическиеПоказателиЛицевыхСчетов.Финансирование КАК Финансирование,
        |    ДинамическиеПоказателиЛицевыхСчетов.Водозабор КАК Водозабор,
        |    ДинамическиеПоказателиЛицевыхСчетов.Договородержатель КАК Договородержатель,
        |    ДинамическиеПоказателиЛицевыхСчетов.Контролер КАК Контролер,
        |    ДинамическиеПоказателиЛицевыхСчетов.Характеристика КАК Характеристика
        |ПОМЕСТИТЬ ВТ_ДинамическиеПоказателиЛицевыхСчетов
        |ИЗ
        |    РегистрСведений.lc_ДинамическиеПоказателиЛицевыхСчетов.СрезПоследних(&Период, ) КАК ДинамическиеПоказателиЛицевыхСчетов
        |ГДЕ
        |	ЛицевойСчет В (ВЫБРАТЬ Т.ЛицевойСчет ИЗ ВТ_ДействующиеЛицевыеСчета КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |    СведенияДляВзаиморасчетовПоЛСАбонентов.Организация КАК Организация,
        |    СведенияДляВзаиморасчетовПоЛСАбонентов.ЛицевойСчет КАК ЛицевойСчет,
        |    СведенияДляВзаиморасчетовПоЛСАбонентов.Договор КАК Договор,
        |    СведенияДляВзаиморасчетовПоЛСАбонентов.Контрагент КАК Контрагент,
        |    ВЫРАЗИТЬ(ВТ_ДинамическиеПоказателиЛицевыхСчетов.ВидПотребления КАК Справочник.lc_ГруппыПотребителей) КАК ВидПотребления,
        |    ВЫРАЗИТЬ(ВТ_ДинамическиеПоказателиЛицевыхСчетов.Финансирование КАК Справочник.lc_СпособыФинансирования) КАК Финансирование,
        |    ВЫРАЗИТЬ(ВТ_ДинамическиеПоказателиЛицевыхСчетов.Водозабор КАК Справочник.lc_Водозаборы)  КАК Водозабор,
        |    ВЫРАЗИТЬ(ВТ_ДинамическиеПоказателиЛицевыхСчетов.Договородержатель КАК Справочник.lc_Контролеры)  КАК Договородержатель,
        |    ВЫРАЗИТЬ(ВТ_ДинамическиеПоказателиЛицевыхСчетов.Контролер КАК Справочник.lc_Контролеры)  КАК Контролер,
        |    ВЫРАЗИТЬ(ВТ_ДинамическиеПоказателиЛицевыхСчетов.Характеристика КАК Справочник.lc_ХарактеристикиОбъекта)  КАК Характеристика
        |ПОМЕСТИТЬ ВТ_СведенияДляВзаиморасчетовИДинПоказатели
        |ИЗ
        |    РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС.СрезПоследних(&Период, ) КАК СведенияДляВзаиморасчетовПоЛСАбонентов
        |    ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДействующиеЛицевыеСчета КАК ВТ_ДействующиеЛицевыеСчета
        |        ПО СведенияДляВзаиморасчетовПоЛСАбонентов.ЛицевойСчет = ВТ_ДействующиеЛицевыеСчета.ЛицевойСчет
        |    ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДинамическиеПоказателиЛицевыхСчетов КАК ВТ_ДинамическиеПоказателиЛицевыхСчетов
        |        ПО СведенияДляВзаиморасчетовПоЛСАбонентов.ЛицевойСчет = ВТ_ДинамическиеПоказателиЛицевыхСчетов.ЛицевойСчет
        |;";

    Запрос.УстановитьПараметр("Период", ДатаАктуальности);
    Запрос.УстановитьПараметр("ТолькоДействующиеЛС", ПараметрТолькоДействующиеЛС.Значение);
    Запрос.Выполнить();

    Возврат МВТ;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
