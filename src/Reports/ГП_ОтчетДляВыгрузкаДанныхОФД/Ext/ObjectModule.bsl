// Гарант+ Килипенко 11.02.2025 [F00232767] Отчет Реестр поступлений по базе "Основная бухгалтерия" ++
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
    СтандартнаяОбработка = Ложь;
    
    ИнициализироватьПараметры();
    
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных,
            ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
    
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    МВТ = СформироватьВременныеТаблицыДляОтчета();
    
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина, Истина, МВТ);

    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий
// Гарант+ Килипенко 11.02.2025 [F00232767] Отчет Реестр поступлений по базе "Основная бухгалтерия" --

// Гарант+ Килипенко 11.02.2025 [F00232767] Отчет Реестр поступлений по базе "Основная бухгалтерия" ++
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьОсновнойНаборДанных()
    Возврат ЭтотОбъект.СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1;
КонецФункции

Процедура ИнициализироватьПараметры()
    // Установка границ периода отчета
    УточнитьПараметрыПериодаОтчета();
КонецПроцедуры

Функция УточнитьПараметрыПериодаОтчета()
    РезультатФункции = Новый Структура("НачалоПериода, КонецПериода", Дата(1, 1, 1), Дата(1, 1, 1));

    ПараметрПериодОтчета =
        БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек, "ПериодОтчета");

    РезультатФункции.НачалоПериода = НачалоДня(ПараметрПериодОтчета.Значение.ДатаНачала);
    РезультатФункции.КонецПериода = КонецДня(ПараметрПериодОтчета.Значение.ДатаОкончания);

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "НачалоПериода", РезультатФункции.НачалоПериода);

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "КонецПериода", РезультатФункции.КонецПериода);

    Возврат РезультатФункции;
КонецФункции

Функция СформироватьВременныеТаблицыДляОтчета(Знач МВТ = Неопределено)
    МВТ = ?(МВТ = Неопределено, Новый МенеджерВременныхТаблиц, МВТ);

    ПараметрНачалоПериода =
        БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек.Настройки, "НачалоПериода");

    ПараметрКонецПериода =
        БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(
            ЭтотОбъект.КомпоновщикНастроек.Настройки, "КонецПериода");
    
    ОбщаяТаблицаРасшифровкиОплатДляОФД = ГП_ОбменОФД.ПолучитьОбщуюТаблицуРасшифровкиОплатДляОФД(
        ПараметрНачалоПериода.Значение, ПараметрКонецПериода.Значение);

    Запрос = Новый Запрос;
    Запрос.МенеджерВременныхТаблиц = МВТ;
    Запрос.Текст =
        "ВЫБРАТЬ
        |   *
        |ПОМЕСТИТЬ ВТ_ОбщаяТаблицаРасшифровкиОплатДляОФД
        |ИЗ
        |   &ОбщаяТаблицаРасшифровкиОплатДляОФД КАК ОбщаяТаблицаРасшифровкиОплатДляОФД
        |;";

    Запрос.УстановитьПараметр("ОбщаяТаблицаРасшифровкиОплатДляОФД", ОбщаяТаблицаРасшифровкиОплатДляОФД);
    Запрос.Выполнить();

    Возврат МВТ;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 11.02.2025 [F00232767] Отчет Реестр поступлений по базе "Основная бухгалтерия" --
