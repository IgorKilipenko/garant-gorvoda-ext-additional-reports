// Гарант+ Килипенко 05.11.2024 [F00230266] Отчет по шифрам ++
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
    СтандартнаяОбработка = Ложь;

    ИнициализироватьПараметры();

    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных,
            ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);

    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    МВТ = Новый МенеджерВременныхТаблиц;

    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина, Истина, МВТ);

    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

// Гарант+ Килипенко 05.11.2024 [F00230266] Отчет по шифрам --

// Гарант+ Килипенко 05.11.2024 [F00230266] Отчет по шифрам ++
#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьПараметры()
    // Установка границ периода отчета
    УточнитьПараметрыПериодаОтчета();

    НомГруппаВодоотведение = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НомГруппаВодоотведение");
    НомГруппаВодоотведение.Значение = Справочники.НоменклатурныеГруппы.НайтиПоКоду("0000002", Истина); // Водоотведение
    НомГруппаВодоотведение.Использование = Истина;

    НоменклатураВодоотведение = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НоменклатураВодоотведение");
    НоменклатураВодоотведение.Значение = Справочники.Номенклатура.НайтиПоКоду("00-00000007", Ложь); // Водоотведение (номенклатура)
    НоменклатураВодоотведение.Использование = Истина;

    НомГруппаВодоснабжение = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НомГруппаВодоснабжение");
    НомГруппаВодоснабжение.Значение =
        Справочники.НоменклатурныеГруппы.НайтиПоКоду("0000001", Истина); // Водоснабжение
    НомГруппаВодоснабжение.Использование = Истина;

    СписокНомГруппНВ = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("СписокНомГруппНВ");
    СписокНомГруппНВ.Значение = Новый СписокЗначений;
    СписокНомГруппНВ.Значение.Добавить(
        Справочники.НоменклатурныеГруппы.НайтиПоКоду(
            "0000158", Истина)); // Негативное воздействие
    СписокНомГруппНВ.Использование = Истина;

    НоменклатураХВС = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НоменклатураХВС");
    НоменклатураХВС.Значение =
        Справочники.Номенклатура.НайтиПоКоду("00-00000003", Ложь); // Холодное водоснабжение (номенклатура)
    НоменклатураХВС.Использование = Истина;

    УслугаПотериВоды = ГП_РаботаСУслугами.ПолучитьУслугуПотериВоды().Ссылка;
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "НоменклатураПотериВоды", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УслугаПотериВоды, "Услуга"));

    НомГруппаНВ = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НомГруппаНВ");
    НомГруппаНВ.Значение =
        Справочники.НоменклатурныеГруппы.НайтиПоКоду("0000158", Истина); // Негативное воздействие
    НомГруппаНВ.Использование = Истина;

    СписокНоменклатурныхГрупп = Новый СписокЗначений;
    СписокНоменклатурныхГрупп.Добавить(НомГруппаВодоснабжение.Значение);
    СписокНоменклатурныхГрупп.Добавить(НомГруппаВодоотведение.Значение);
    СписокНоменклатурныхГрупп.Добавить(НомГруппаНВ.Значение); // Добавлено по изм. ТЗ

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "СписокНоменклатурныхГрупп", СписокНоменклатурныхГрупп);
КонецПроцедуры

Функция УточнитьПараметрыПериодаОтчета()
    РезультатФункции = Новый Структура("ДатаНачала, ДатаОкончания", Дата(1, 1, 1), Дата(1, 1, 1));

    ПараметрПериодОтчета =
        ГП_РаботаСОтчетами.ПолучитьПараметрПоНаименованию(ЭтотОбъект, "ПериодОтчета");

    РезультатФункции.ДатаНачала = НачалоДня(ПараметрПериодОтчета.Значение.ДатаНачала);
    РезультатФункции.ДатаОкончания = КонецДня(ПараметрПериодОтчета.Значение.ДатаОкончания);

    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "НачалоПериодаГраница", Новый Граница(РезультатФункции.ДатаНачала, ВидГраницы.Включая));
    БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
        ЭтотОбъект.КомпоновщикНастроек.Настройки, "КонецПериодаГраница", Новый Граница(РезультатФункции.ДатаОкончания, ВидГраницы.Включая));

    Возврат РезультатФункции;
КонецФункции

// Устарела. Временное решение проблемы отсутствия полей / регистров в расширении - в версии платформы 29 проблема должна быть решена
//
Процедура Удалить_ПодготовитьТекстЗапроса()
    ТекстЗапроса = ЭтотОбъект.СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Запрос;

    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_ДинамическиеПоказателиЛицевыхСчетов###",
            "РегистрСведений.lc_ДинамическиеПоказателиЛицевыхСчетов.СрезПоследних(&КонецПериодаГраница, )");

    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СоответствияНазначенийПлатежей###",
            "РегистрСведений.ГП_СоответствияНазначенийПлатежей.СрезПоследних(&КонецПериодаГраница, )");

    СимволыКонцаВЫражения = "(?:[\s\n\r]+)";
    ТекстЗапроса = СтрЗаменитьПоРегулярномуВыражению(ТекстЗапроса,
            "(?m)\(?ВТ_Результат.Водозабор = &Водозабор\)?," + СимволыКонцаВЫражения
            + "\(?ВТ_Результат.Контролер = &Контролер\)?," + СимволыКонцаВЫражения
            + "\(?ВТ_Результат.Договородержатель = &Договородержатель\)?," + СимволыКонцаВЫражения
            + "\(?ВТ_Результат.Финансирование = &Финансирование\)?," + СимволыКонцаВЫражения
            + "\(?ВТ_Результат.ВидПотребления = &ВидПотребления\)?," + СимволыКонцаВЫражения,

            "(ВЫРАЗИТЬ(ВТ_Результат.Водозабор КАК Справочник.lc_Водозаборы)) = &Водозабор,
            |   (ВЫРАЗИТЬ(ВТ_Результат.Контролер КАК Справочник.lc_Контролеры)) = &Контролер,
            |   (ВЫРАЗИТЬ(ВТ_Результат.Договородержатель КАК Справочник.lc_Контролеры)) = &Договородержатель,
            |   (ВЫРАЗИТЬ(ВТ_Результат.Финансирование КАК Справочник.lc_СпособыФинансирования)) = &Финансирование,
            |   (ВЫРАЗИТЬ(ВТ_Результат.ВидПотребления КАК Справочник.lc_ГруппыПотребителей)) = &ВидПотребления,
            |");

    ЭтотОбъект.СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Запрос = ТекстЗапроса;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 05.11.2024 [F00230266] Отчет по шифрам --
