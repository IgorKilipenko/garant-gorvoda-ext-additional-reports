// ->>> Гарант+ Килипенко 27.05.2025 [F00238494] Учет 68.10 в отчете РасчетыПоЕСН
#Область ПереопределениеСтандартногоФункционала

&ИзменениеИКонтроль("ПередКомпоновкойМакета")
Процедура ГП_ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек)

	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"Организация",
		ПараметрыОтчета.Организация);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"НачалоПериода",
		НачалоДня(ПараметрыОтчета.НачалоПериода));

	КонецПериода = ?(ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода),
		КонецДня(ПараметрыОтчета.КонецПериода),
		'39990101');
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"КонецПериода",
		КонецПериода);

	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"РуководительДолжность",
		ОтветственныеЛица.РуководительДолжность);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"РуководительПредставление",
		ОтветственныеЛица.РуководительПредставление);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"ТекущаяДата",
		ОбщегоНазначенияБП.ТекущаяДатаНаСервере());

	ПростойУчетЕНС = ПараметрыОтчета.НачалоПериода >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета();

	Если ПараметрыОтчета.ВариантОтчета = "РасчетыПоЕНС"
		И Не ПростойУчетЕНС Тогда

		ИмяОтчетаРасчетыПоНалогам = НСтр("ru='Расшифровка расчетов налогов по ЕНС'");
		ПериодОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
		ОстатокПоЕНСНаНачалоПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, ПараметрыОтчета.НачалоПериода);
		ОстатокПоЕНСНаКонецПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);

		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ПериодОтчета",
			ПериодОтчета);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ОстатокПоЕНСНаНачалоПериода",
			Формат(ОстатокПоЕНСНаНачалоПериода, "ЧДЦ=2; ЧН=0,00"));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ОстатокПоЕНСНаКонецПериода",
			Формат(ОстатокПоЕНСНаКонецПериода, "ЧДЦ=2; ЧН=0,00"));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ИмяОтчетаРасчетыПоНалогам",
			ИмяОтчетаРасчетыПоНалогам);

	КонецЕсли;

	Если ПараметрыОтчета.ВариантОтчета = "РасчетыПоНалогамНаЕНС"
		И ПростойУчетЕНС Тогда

		// Все обслуживаемые счета, кроме 68.10, для него есть отдельный запрос
		СчетаНалоговВзносов = ЕдиныйНалоговыйСчетПовтИсп.ОбслуживаемыеСчетаУчета(ПараметрыОтчета.КонецПериода);
		СчетаНалоговВзносов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СчетаНалоговВзносов,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыСчетов.Хозрасчетный.ПрочиеНалогиИСборы));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"СчетаНалоговВзносов",
			СчетаНалоговВзносов);
        #Вставка // ->>> Гарант+ Килипенко 27.05.2025 [F00238494] Учет 68.10 в отчете РасчетыПоЕСН

        // Дополнение запроса данными по КорСчету 68.10 (СчетПрочихНалогов) где нет субконто с Видом ВидыНалогов
        СтруктураНабораДанныхУплатыНалогов = ПолучитьНаборДанныхНачислениеУплатаНалогов(Схема);
        Если СтруктураНабораДанныхУплатыНалогов.Успех = Истина Тогда
            СтруктураНабораДанныхУплатыНалогов.НаборДанных.Запрос = СтрЗаменить(СтруктураНабораДанныхУплатыНалогов.НаборДанных.Запрос,
                "// ###ВСТАВКА #ГП_ТаблицаСчетПрочихНалоговБезВидаНалога",
                "ОБЪЕДИНИТЬ ВСЕ
                |
                |ВЫБРАТЬ
                |   ХозрасчетныйОбороты.Организация КАК Организация,
                |   ХозрасчетныйОбороты.Период КАК Период,
                |   ХозрасчетныйОбороты.Регистратор КАК Регистратор,
                |   ХозрасчетныйОбороты.КорСчет КАК СчетНалога,
                |   НЕОПРЕДЕЛЕНО КАК Налог,
                |   ХозрасчетныйОбороты.СуммаОборотКт КАК Начислено,
                |   0 КАК Уплачено
                |ИЗ
                |   РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Регистратор,
                |       Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет), , {(Организация).* КАК Организация},
                |       Корсчет В (&СчетПрочихНалогов),) КАК ХозрасчетныйОбороты
                |ГДЕ
                |   НЕ (Организация, Период, Регистратор) В (
                |       ВЫБРАТЬ
                |           Т.Организация КАК Организация,
                |           Т.Период КАК Период,
                |           Т.Регистратор КАК Регистратор
                |       ИЗ
                |           РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Регистратор,
                |               Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет), , {(Организация).* КАК Организация},
                |               Корсчет В (&СчетПрочихНалогов), &ВидСубконтоВидыНалогов) КАК Т
                |   )
                |
                |");
        КонецЕсли;
        #КонецВставки // <<<- Гарант+ Килипенко 27.05.2025 [F00238494] Учет 68.10 в отчете РасчетыПоЕСН

		ПоказыватьСчетаУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
		Если Не ПоказыватьСчетаУчета Тогда
			СтруктураОтчета = КомпоновщикНастроек.Настройки.Структура;
			ГруппировкаНалог = БухгалтерскиеОтчеты.НайтиПоИмени(СтруктураОтчета, "Налог");
			ОтключаемыеПоля = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый ПолеКомпоновкиДанных("СчетНалога"));
			ОтключитьПоля(ГруппировкаНалог, ОтключаемыеПоля);
		КонецЕсли;

	КонецЕсли;

	Если ПараметрыОтчета.ВариантОтчета = "ВедомостьПоЕНС"
		И ПростойУчетЕНС Тогда

		СчетаНалоговВзносов = ЕдиныйНалоговыйСчетПовтИсп.ОбслуживаемыеСчетаУчета(ПараметрыОтчета.КонецПериода);
		СчетаНалоговВзносов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СчетаНалоговВзносов,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыСчетов.Хозрасчетный.ПрочиеНалогиИСборы));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"СчетаНалоговВзносов",
			СчетаНалоговВзносов);

		ОстатокПоЕНСНаНачалоПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, НачалоДня(ПараметрыОтчета.НачалоПериода) - 1);
		ОстатокПоЕНСНаКонецПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);

		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ОстатокПоЕНСНаНачалоПериода",
			ОстатокПоЕНСНаНачалоПериода);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ОстатокПоЕНСНаКонецПериода",
			ОстатокПоЕНСНаКонецПериода);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ПереопределениеСтандартногоФункционала
// <<<- Гарант+ Килипенко 27.05.2025 [F00238494] Учет 68.10 в отчете РасчетыПоЕСН

// ->>> Гарант+ Килипенко 27.05.2025 [F00238494] Учет 68.10 в отчете РасчетыПоЕСН
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьНаборДанныхНачислениеУплатаНалогов(Знач СхемаКомпоновки)
    РезультатФункции = Новый Структура("Успех, НаборДанных, ТекстСообщения", Истина);

    НаименованиеНабора = "НачислениеУплатаНалогов";
    НайденныйНабор = СхемаКомпоновки.НаборыДанных.Найти("НачислениеУплатаНалогов");
    Если НайденныйНабор = Неопределено ИЛИ ТипЗнч(НайденныйНабор) <> Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон("Не найден набор данных << %1 >>", НаименованиеНабора);
        Возврат РезультатФункции; // Не найден набор данный
    КонецЕсли;

    РезультатФункции.НаборДанных = НайденныйНабор;
    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// <<<- Гарант+ Килипенко 27.05.2025 [F00238494] Учет 68.10 в отчете РасчетыПоЕСН
