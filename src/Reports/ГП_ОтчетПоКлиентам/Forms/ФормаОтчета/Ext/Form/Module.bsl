// Гарант+ Килипенко 09.10.2024 [F00228925] Отчет по клиентам ++
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗагрузкойПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)
    ВыполнитьИнициализациюЭлементовОтбораНаСервере(Истина, Настройки);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    Если Отказ Тогда
        Возврат;
    КонецЕсли;

    ВыполнитьИнициализациюЭлементовОтбораНаСервере(Ложь);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 09.10.2024 [F00228925] Отчет по клиентам --

// Гарант+ Килипенко 09.10.2024 [F00228925] Отчет по клиентам ++
#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЭлементФормыКомпоновкиНастроекПоЗаголовку(Знач Форма, Знач Заголовок)
    Для Каждого ТекущийЭлемент Из Форма.Элементы Цикл
        ЭлементНайден = ТекущийЭлемент.Заголовок = Заголовок;
        ЭлементНайден = ЭлементНайден И СтрНачинаетсяС(ТекущийЭлемент.Имя, "КомпоновщикНастроекПользовательскиеНастройкиЭлемент");
        ЭлементНайден = ЭлементНайден И СтрЗаканчиваетсяНа(ТекущийЭлемент.Имя, "Значение");

        Если ЭлементНайден Тогда
            Возврат ТекущийЭлемент;
        КонецЕсли;
    КонецЦикла;

    Возврат Неопределено;
КонецФункции

&НаСервере
Процедура ВыполнитьИнициализациюЭлементовОтбораНаСервере(Знач УстанавливатьЗначенияПоУмолчанию = Истина, Знач Настройки = Неопределено)
    СтруктураЭлементовДляНастройки = Новый Структура;
    КлючиСтруктуры = "Имя, Значение";
    СтруктураЭлементовДляНастройки.Вставить("Водозабор",
        Новый Структура(КлючиСтруктуры, "Водозабор", Справочники.lc_Водозаборы.ПустаяСсылка()));
    СтруктураЭлементовДляНастройки.Вставить("Контролер",
        Новый Структура(КлючиСтруктуры, "Контролер", Справочники.lc_Контролеры.ПустаяСсылка()));
    СтруктураЭлементовДляНастройки.Вставить("Финансирование",
        Новый Структура(КлючиСтруктуры, "Финансирование", Справочники.lc_СпособыФинансирования.ПустаяСсылка()));

    // !!! Временное решение из-за отсутствия типов в расширении
    Если УстанавливатьЗначенияПоУмолчанию Тогда
        Попытка
            Для Каждого ПараметрОтбора Из Настройки.Элементы Цикл
                Если ПараметрОтбора.Параметр = Новый ПараметрКомпоновкиДанных(СтруктураЭлементовДляНастройки.Водозабор.Имя) Тогда
                    ПараметрОтбора.Значение = СтруктураЭлементовДляНастройки.Водозабор.Значение;
                ИначеЕсли ПараметрОтбора.Параметр = Новый ПараметрКомпоновкиДанных(СтруктураЭлементовДляНастройки.Контролер.Имя) Тогда
                    ПараметрОтбора.Значение = СтруктураЭлементовДляНастройки.Контролер.Значение;
                ИначеЕсли ПараметрОтбора.Параметр = Новый ПараметрКомпоновкиДанных(СтруктураЭлементовДляНастройки.Финансирование.Имя) Тогда
                    ПараметрОтбора.Значение = СтруктураЭлементовДляНастройки.Финансирование.Значение;
                КонецЕсли;
            КонецЦикла;
        Исключение
            // Без сообщения
        КонецПопытки;
    КонецЕсли;

    Для Каждого ЭлементКЗ Из СтруктураЭлементовДляНастройки Цикл
        ТекущийЭлементОтбора = ПолучитьЭлементФормыКомпоновкиНастроекПоЗаголовку(ЭтотОбъект, ЭлементКЗ.Значение.Имя);
        Если ТекущийЭлементОтбора = Неопределено Тогда
            Продолжить;
        КонецЕсли;

        ТекущийЭлементОтбора.ДоступныеТипы = Новый ОписаниеТипов(
            ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипЗнч(ЭлементКЗ.Значение.Значение)));
        ТекущийЭлементОтбора.ВыбиратьТип = Ложь;
    КонецЦикла;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
